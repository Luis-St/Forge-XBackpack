import java.nio.file.Files

plugins {
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.minecraftforge.gradle" version "[6.0,6.2)"
}

minecraft {
	mappings channel: "official", version: MinecraftVersion
	copyIdeResources = true
	accessTransformer = file("src/main/resources/META-INF/accesstransformer.cfg")
	
	runs {
		client {
			workingDirectory project.file("run")
			
			property "forge.logging.markers", "REGISTRIES"
			property "forge.logging.console.level", "debug"
			property "forge.enabledGameTestNamespaces", "xbackpack"
			property "forge.logging.mojang.level", "debug"
			
			mods {
				examplemod {
					source sourceSets.main
				}
			}
		}
		
		server {
			workingDirectory project.file("run")
			
			property "forge.logging.markers", "REGISTRIES"
			property "forge.logging.console.level", "debug"
			property "forge.enabledGameTestNamespaces", "xbackpack"
			property "forge.logging.mojang.level", "debug"
			
			mods {
				xbackpack {
					source sourceSets.main
				}
			}
		}
		
		gameTestServer {
			workingDirectory project.file("run")
			
			property "forge.logging.markers", "REGISTRIES"
			property "forge.logging.console.level", "debug"
			property "forge.enabledGameTestNamespaces", "xbackpack"
			property "mixin.env.remapRefMap", "true"
			property "mixin.env.refMapRemappingFile", "${buildDir}/createSrgToMcp/output.srg"
			
			mods {
				xbackpack {
					source sourceSets.main
				}
			}
		}
		
		data {
			workingDirectory project.file("run")
			
			property "forge.logging.markers", "REGISTRIES"
			property "forge.logging.console.level", "debug"
			
			args "--mod", "xbackpack", "--all", "--output", file("src/generated/resources/"), "--existing", file("src/main/resources/")
			
			mods {
				xbackpack {
					source sourceSets.main
				}
			}
		}
	}
}

sourceSets.main.resources {srcDir "src/generated/resources"}

repositories {
	maven {
		name = "ModMaven"
		url = "https://modmaven.dev/"
	}
}

dependencies {
	minecraft "net.minecraftforge:forge:${MinecraftVersion}-${ForgeVersion}"
	
	compileOnly fg.deobf("mezz.jei:jei-${MinecraftVersion}-common-api:${JeiVersion}")
	compileOnly fg.deobf("mezz.jei:jei-${MinecraftVersion}-forge-api:${JeiVersion}")
	runtimeOnly fg.deobf("mezz.jei:jei-${MinecraftVersion}-forge:${JeiVersion}")
}

java {
	withSourcesJar()
}

artifacts {
	archives sourcesJar
}

def username = ""
def password = ""
def creds = file("./../../credentials.properties")
if (creds.exists()) {
	def properties = new Properties()
	creds.withInputStream {properties.load(it)}
	username = properties.getProperty("username")
	password = properties.getProperty("password")
}

def ver = "${MinecraftVersion}-${ModVersion}"
tasks.register("renameArtifacts") {
	doLast {
		def jar = file("build/libs/${project.name}.jar")
		def sourcesJar = file("build/libs/${project.name}-sources.jar")
		if (Files.exists(jar.toPath())) {
			def folder = file("D:/Games/Forge/Uploaded/XBackpack/${MinecraftVersion}")
			if (!Files.exists(folder.toPath())) {
				Files.createDirectory(folder.toPath())
			}
			if (!Files.exists(folder.toPath().resolve("XBackpack-${ver}.jar"))) {
				Files.move(jar.toPath(), folder.toPath().resolve("XBackpack-${ver}.jar"))
				println "Moved '${jar}' to '${folder.toPath().resolve("XBackpack-${ver}.jar")}'"
			} else {
				Files.delete(jar.toPath())
				println "Deleted '${jar}'"
			}
		}
		if (Files.deleteIfExists(sourcesJar.toPath())) {
			println "Deleted '${sourcesJar}'"
		}
	}
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId = "net.luis"
			artifactId = "XBackpack"
			version = ver
			artifact jar
			artifact sourcesJar
		}
	}
	repositories {
		if (!username.isBlank() && !password.isBlank()) {
			maven {
				url "https://maven.luis-st.net/forge/"
				credentials.username = username
				credentials.password = password
			}
		} else {
			System.err.println("No credentials provided. Publishing to maven.luis-st.net not possible.")
		}
	}
}

def resourceTargets = ["META-INF/mods.toml", "pack.mcmeta"]
def replaceProperties = [
	MinecraftVersion: MinecraftVersion, MinecraftVersionRange: MinecraftVersionRange,
	ForgeVersion: ForgeVersion, ForgeVersionRange: ForgeVersionRange,
	ForgeLoaderRange: ForgeLoaderRange, ModVersion: ModVersion
]
processResources {
	inputs.properties replaceProperties
	replaceProperties.put "project", project
	
	filesMatching(resourceTargets) {
		expand replaceProperties
	}
}

jar {
	manifest {
		attributes([
			"Specification-Title": "XBackpack",
			"Specification-Version": "${ModVersion}",
			"Implementation-Title": project.name,
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd HH:mm:ssZ")
		])
	}
	finalizedBy("reobfJar")
}

sourcesJar {
	outputs.upToDateWhen {false}
}

build.finalizedBy("renameArtifacts")
publish.finalizedBy("renameArtifacts")

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

sourceSets.each {
	def dir = layout.buildDirectory.dir("sourcesSets/${it.name}")
	it.output.resourcesDir = dir
	it.java.destinationDirectory = dir
}
